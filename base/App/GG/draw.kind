// Renders App
App.GG.draw(img: VoxBox): App.Draw<App.GG.State>
  (state)
  let local = state@local
  case local {
    lobby: App.GG.Lobby.draw(local.state)
    game:  App.GG.draw.game(local.state, img)
  }


App.GG.draw.game(
  game: App.GG.Game
  img: VoxBox
): DOM
  <div style={
    "height": "max(100%, 600px)"
    "width": "100vw"
    "display": "flex",
    "background-color": "#17191d",
    "align-items": "center",
    "justify-content": "center"
  }>
    <div style={
        "max-height": "100%",
        "max-width": "100%",
        "overflow": "hidden",
        "margin-top": "-512px",
      }>{
        DOM.vbox({
            "class": "pixel-art",
            "id": "game_screen",
            "width": "576",
            "height": "480",
            "scale": "2"
          },
          {},
          let bar = case game@phase as phase {setup: 0#64, game: phase.turn_bar} 
          let img = App.GG.draw.map(img)
          let img = App.GG.draw.grid(game, img)
          let img = App.GG.draw.turn_bar(bar, img)
          App.GG.draw.setup(game@phase,img))
      }</div>
  </div>

App.GG.draw.map(img: VoxBox): VoxBox
  let map =
    [
      {{0#32,   0#32  }, App.GG.img.map.0x0}
      {{256#32, 0#32  }, App.GG.img.map.1x0}
      {{512#32, 0#32  }, App.GG.img.map.2x0}
      {{0#32,   256#32}, App.GG.img.map.0x1}
      {{256#32, 256#32}, App.GG.img.map.1x1}
      {{512#32, 256#32}, App.GG.img.map.1x2}
    ]
  let f =
    (x: U32, y: U32, i: VoxBox, j: VoxBox)
     VoxBox.Draw.image(x, y, 0, i, j)
  VoxBox.Large.draw(f, map, img)

App.GG.draw.setup(phase: App.GG.Phase, img: VoxBox): VoxBox
  case phase {
    setup: 
      let {sx, sy} = App.GG.Constants.map_starting_position
      let {cx, cy} = App.GG.Constants.chips_starting_position
      let {x,  y } = {cx + sx, cy + sy}
      let img = VoxBox.Draw.image((x - 128) + 22, (y - 128) + 23, 150, App.GG.img.background, img)
      let selected = App.GG.Table.Slot.get_selected_index(phase.table)
      let img = App.GG.draw.setup.chips(0, phase.table@slots, selected, x, y - 27, img)
      let img = VoxBox.Draw.text(I32.show(phase.energy), PixelFont.black, Pos32.new(x + 70, y + 60,200), img)
      let bar = I32.to_u32((phase.energy * 64) / 100)
      let img = VoxBox.Draw.rect(x + 70, (y + 55) - bar, 240, 11, bar, Col32.new(240, 220, 0, 255), img)
      img
    game: img
  }
//40x22
App.GG.draw.turn_bar(turn_bar: U64, img: VoxBox): VoxBox
  let tile_size = 32#32
  let map_x = App.GG.Constants.grid_size@fst
  let proportion = 1024#32 / (tile_size * map_x)
  let max = U32.min(F64.to_u32(U64.to_f64(turn_bar)), 1024)
  let {sx, sy} = App.GG.Constants.map_starting_position
  VoxBox.Draw.rect(sx, sy - 5, 100, max / proportion, 4, Col32.new(100, 255, 100, 255), img)

App.GG.draw.setup.chips(
  count: U32, 
  slots: List<App.GG.Table.Slot>, 
  selected: Maybe<U32>,
  x: U32, 
  y: U32, 
  img: VoxBox
): VoxBox
  case slots {
    nil : img
    cons:
      // get constants
      let {cx, cy} = App.GG.Constants.chip_size
      let {vx, vy} = App.GG.Constants.chips_space_between
      let max_stack = App.GG.Constants.chips_array@snd
      // calculate x and y
      let {x, y} = 
        if ((count + 1) % (max_stack + 1)) =? 0 then 
          {x + (cx + vx), y - ((cy + vy) * max_stack)} 
        else 
          {x, y} 
      let {tx, ty} = {(x + 2) - (App.GG.Constants.chip_size@fst / 2), (y + 4) - (App.GG.Constants.chip_size@snd / 2)}

      // draw user selection if chip is selected
      let img = App.GG.draw.setup.chips.selected_chip(
        x, y, count, selected, img
      )
      // draw chips
      let {tx, ty} = {(x + 2) - (App.GG.Constants.chip_size@fst / 2), (y + 4) - (App.GG.Constants.chip_size@snd / 2)} // get text position
      let img = Maybe {
        get chip = slots.head@chip
        let img = VoxBox.Draw.image_colorize(x - 128, y - 128, 200, Col32.new_hue(App.GG.Class.to_hue(chip@class)) App.GG.img.back_w_outline_brighter, img)
        // let img = VoxBox.Draw.image(x - 128, y - 128, 200, chip@img, img)
        let img = VoxBox.Draw.text(chip@name, PixelFont.black, Pos32.new(tx,ty,200), img)
        return img
      } <> img
      // recursion
      App.GG.draw.setup.chips(count + 1, slots.tail, selected, x, y + (cy + vy), img)
  }

      // case table.head@chip as chip {
      //   none: App.GG.draw.setup.chips(count + 1, table.tail, x, y + (cy + vy), img)
      //   some:
      //     let img = VoxBox.Draw.image_colorize(x - 128, y - 128, 200, Col32.new_hue(chip.value@hue) App.GG.img.back_w_outline_brighter, img)
      //     let img = VoxBox.Draw.text(chip.value@name, PixelFont.black, Pos32.new(tx,ty,200), img)


App.GG.draw.setup.chips.selected_chip(
  x: U32
  y: U32
  count: U32
  selected: Maybe<U32>
  img: VoxBox
): VoxBox
  Maybe {
    get selected = selected
    let img = 
      if selected =? count then 
        VoxBox.Draw.image(x - 128, y - 128, 250, App.GG.img.outline_selected, img)
      else 
        img
    return img
  } <> img

// App.GG.draw.setup.chip(chip: App.GG.Chip, x: U32, y: U32, img:VoxBox): VoxBox
//   VoxBox.Draw.image(x, y, 200, chip@img, img)



// Draws the grid
App.GG.draw.grid(
  game: App.GG.Game
  img: VoxBox
): VoxBox
  for key:val in game@grid with img:
    App.GG.draw.tile(key, val, {32,32}, game, img)
  img

// Draws a tile and its contents
App.GG.draw.tile(
  coord: App.GG.Coord,
  tile: App.GG.Tile,
  tile_size: Pair<U32, U32>,
  game: App.GG.Game
  img: VoxBox
): VoxBox
  let {x, y} = App.GG.Coord.to_xy(coord)
  let tile_img = 
    case tile@statuses@reserved {
      none:
        case tile@owner {
          blue:     App.GG.img.ally_tile
          red:      App.GG.img.enemy_tile
          neutral:  App.GG.img.target_tile
        }
      some:
        VoxBox.empty
    }
  let creature = tile@creature
  let img = 
    VoxBox.Draw.image(x, y, 0, tile_img, img)
  let img =
    case creature {
      none:
        img
      some:
        let f = App.GG.Monster.to_vbox(creature.value@category)
        let sprite = f(creature.value@animation@fst, creature.value@animation@snd)
        let vbox = App.GG.Creature.Status.draw(creature.value@statuses, {x, y})
        //let img = VoxBox.Draw.image_colorize(x, y, 20, Col32.new_hue(F64.to_u32(U64.to_f64((creature.value@animation@snd % 1024) / 4))), sprite, img)
        let img = 
          if App.GG.Creature.Status.intangible_draw_invisible(creature.value@statuses) then
            img
          else
            if App.GG.Team.eql(App.GG.Team.red, creature.value@team) then
              case creature.value@category as monster {
                //minion: VoxBox.Draw.image_colorize_reverse_x(x, y, 20, Col32.new_hue(Nat.to_u32(monster.data@level * 50) + 12), sprite, img)
                minion: VoxBox.Draw.image_reverse_x(x, y, 20, sprite, img)
                slime: VoxBox.Draw.image_colorize_reverse_x(x, y, 20, Col32.new_hue(12), sprite, img)
              }
            else VoxBox.Draw.image(x, y, 20, sprite, img)
        let img = VoxBox.Draw.text(I32.show(creature.value@hp), PixelFont.small_black, Pos32.new(x + 122,y + 100, 100), img)
        let img = vbox(img)
        let img = 
          let actions = creature.value@actions
          let current = actions@current
          without current: img
          open current
          let result = current@effect(coord, App.GG.Effect.Origin.creature, game)
          case result {
            err: img
            new: result.img(App.GG.Team.eql(App.GG.Team.red, creature.value@team), img)
          }
        img
    }
  
  let pair = {0, img}
  for projectile in tile@projectiles with pair:
    let {idx, img} = pair
    let origin = App.GG.Effect.Origin.projectile(idx)
    let result = projectile@effect(coord, origin, game)
    case result {
      err: {idx + 1, img}
      new: {idx + 1, result.img(App.GG.Team.eql(App.GG.Team.red, projectile@info@team), img)}
    }
  img

App.GG.draw.image(x: U32, y: U32, z: U32, img: VoxBox, enemy: Bool, src: VoxBox): VoxBox
  let f = 
    if enemy then
      VoxBox.Draw.image_reverse_x
    else
      VoxBox.Draw.image
  f(x, y, z, img, src)
